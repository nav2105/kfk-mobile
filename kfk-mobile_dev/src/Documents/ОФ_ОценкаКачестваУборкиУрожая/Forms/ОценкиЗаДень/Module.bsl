
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Период = ТекущаяДата();
	ЗаполнитьОценкиЗаПериод();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ЗаписьОценкаКачества" Тогда 
		ЗаполнитьОценкиЗаПериод();	
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОценкиЗаПериод()

	ОценкиКачества.Очистить();

	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ОценкиКачества.Бригада,
	|	СУММА(ОценкиКачества.КоличествоНесоответствий) / СУММА(ОценкиКачества.РазмерОбъединеннойПробы) * 100 КАК ПроцентНессответсвий,
	|	100 - (СУММА(ОценкиКачества.КоличествоНесоответствий) / СУММА(ОценкиКачества.РазмерОбъединеннойПробы) * 100) КАК ПроцентСоответствия,
	|	СУММА(ОценкиКачества.РазмерОбъединеннойПробы) КАК РазмерОбъединеннойПробы,
	|	КОЛИЧЕСТВО(ОценкиКачества.Ссылка) КАК КоличествоПроб
	|ИЗ
	|	Документ.ОФ_ОценкаКачестваУборкиУрожая КАК ОценкиКачества
	|ГДЕ
	|	НЕ ОценкиКачества.ПометкаУдаления
	|	И ОценкиКачества.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|СГРУППИРОВАТЬ ПО
	|	ОценкиКачества.Бригада");

	Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(Период));
	Запрос.УстановитьПараметр("КонецПериода",  КонецДня(Период));

	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ОценкиКачества.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		Оценка = ОценкаКачестваКлиентСервер.ПолучитьОценку(НоваяСтрока.ПроцентСоответствия);
		Если Оценка > 0 И Оценка <=5 Тогда   
			НоваяСтрока.КартинкаСтроки = БиблиотекаКартинок["Цифра" + Строка(Оценка)];
		Иначе
			НоваяСтрока.КартинкаСтроки = Новый Картинка;	
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ОбновитьЗаголовокФормы()

	Заголовок = Формат(Период, "ДФ='dd MMMM';")

КонецПроцедуры

&НаКлиенте
Процедура ОценкиКачестваВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

КонецПроцедуры

&НаКлиенте
Процедура ДекорацияНазадНажатие(Элемент)

	Период = Период - 86400;
	ЗаполнитьОценкиЗаПериод();

КонецПроцедуры

&НаКлиенте
Процедура ДекорацияВпередНажатие(Элемент)
	
	Период = Период + 86400;
	ЗаполнитьОценкиЗаПериод();
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	ЗаполнитьОценкиЗаПериод();
	
КонецПроцедуры





