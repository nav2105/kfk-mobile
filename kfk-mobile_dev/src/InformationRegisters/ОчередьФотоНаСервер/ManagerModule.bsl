

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ДобавитьФото(Документ, ИндексФото, АдресНаДиске) Экспорт
	
	НовыйЭлементОчереди = РегистрыСведений.ОчередьФотоНаСервер.СоздатьМенеджерЗаписи();

	НовыйЭлементОчереди.Документ        = Документ;
	НовыйЭлементОчереди.ИндексФото 		= ИндексФото;
	НовыйЭлементОчереди.АдресНаДиске 	= АдресНаДиске;

	НовыйЭлементОчереди.Записать();
	
	Константы.ЕстьНеотправленныеФото.Установить(Истина);
	
КонецПроцедуры

Процедура УдалитьСообщение(Документ, ИндексФото) Экспорт

	НаборЗаписей = РегистрыСведений.ОчередьФотоНаСервер.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Документ.Установить(Документ);
	НаборЗаписей.Отбор.ИндексФото.Установить(ИндексФото);
	НаборЗаписей.Записать();

	Выборка = РегистрыСведений.ОчередьФотоНаСервер.Выбрать();
	Если Выборка.Следующий() Тогда
		Константы.ЕстьНеотправленныеФото.Установить(Истина);
	Иначе
		Константы.ЕстьНеотправленныеФото.Установить(Ложь);		
	КонецЕсли;

КонецПроцедуры

Функция МассивСообщений() Экспорт
	
	МассивСообщения = Новый Массив;
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ОчередьФотоНаСервер.Документ КАК Документ,
	                      |	ОчередьФотоНаСервер.Документ.Дата КАК Дата,
	                      |	ОчередьФотоНаСервер.ИндексФото КАК ИндексФото,
	                      |	ОчередьФотоНаСервер.АдресНаДиске КАК АдресНаДиске
	                      |ИЗ
	                      |	РегистрСведений.ОчередьФотоНаСервер КАК ОчередьФотоНаСервер");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл 
		СтруктураСообщения = Новый Структура;
		ДвоичныеДанные = Новый ДвоичныеДанные(Выборка.АдресНаДиске);
														
		СтруктураСообщения.Вставить("Документ", 		Выборка.Документ);
		СтруктураСообщения.Вставить("СсылкаСтрокой", 	Строка(Выборка.Документ.УникальныйИдентификатор()));
		СтруктураСообщения.Вставить("ИндексФото", 		Выборка.ИндексФото);
		СтруктураСообщения.Вставить("ИндексСтрокой", 	Формат(Выборка.ИндексФото, "ЧФ=Ч"));
		СтруктураСообщения.Вставить("Данные", 			ДвоичныеДанные);
		СтруктураСообщения.Вставить("Дата", 			Выборка.Дата);
		СтруктураСообщения.Вставить("АдресНаДиске", 	Выборка.АдресНаДиске);
		
		МассивСообщения.Добавить(СтруктураСообщения);	
	КонецЦикла;
	
	Возврат МассивСообщения;
	
КонецФункции


#КонецОбласти

#КонецЕсли
